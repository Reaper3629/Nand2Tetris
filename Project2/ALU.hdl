// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // Checks to zero and/or negate the x input 
    Mux16(a=x,b[0..15]=false,sel=zx,out=out1);
    Not16(in=out1,out=nOut1);
    Mux16(a=out1,b=nOut1, sel=nx, out=out2);   

    // Y-version
    Mux16(a=y,b[0..15]=false,sel=zy,out=out3);
    Not16(in=out3,out=nOut3);
    Mux16(a=out3,b=nOut3, sel=ny, out=out4);  

    // Mux that checks for either adding or AND-ing the x & y inputs
    Add16(a=out2, b=out4,out=TwoAddFour);
    And16(a=out2, b=out4,out=TwoAndFour);

    Mux16(a=TwoAndFour,b=TwoAddFour,sel=f,out=out5);

    // Mux that checks for negative the output
    Not16(in=out5,out=nOut5);
    Mux16(a=out5,b=nOut5,sel=no,out=out);
    
    // Secondary Mux16 to get ALU output bits
    // ALU Negative Output; checks whether the output is negative
    Mux16(a=out5,b=nOut5,sel=no,out=out6,out[15]=ng,out[0..7]=out6p1,out[8..15]=out6p2);


    // ALU Zero Output; checks whether the output is zero
    Or8Way(in=out6p1,out=orOut6p1);
    Or8Way(in=out6p2,out=orOut6p2);
    Or(a=orOut6p1,b=orOut6p2,out=nZr);
    Not(in=nZr,out=zr);  
}